# Arithmetics

function add(a,b)
	b ~> a
	return a
end

function sub(a,b)
	while b
		b-
		a-
	end
	return a
end

function mul(a,b | tmp)
	
	0 -> tmp					# with tmp = 0
	while a						# a times do
		a-						
		add(b,tmp) -> tmp		# tmp += b
	end
	
	return tmp
end

function pow(a,b | tmp)
	
	1 -> tmp					# with tmp = 1
	while a						# a times do
		a-
		mul(b,tmp) -> tmp		# tmp *= b
	end
	
	return tmp
end

function mul2(a | b)
	a -> b
	b ~> a
	return a
end

function mul4(a | b)
	a ~> b,b,b,b
	b ~> a
	return a
end

function pow2(n | tmp)
	1 -> tmp
	while n
		n-
		mul2(tmp) -> tmp?
	end
	return tmp
end

# Booleans and logic

function true(| res)
	1 -> res
	return res
end

function false(| res)
	0 -> res
	return res
end

function not(bool | res)

	1 -> res
	
	while bool
		0 -> bool
		0 -> res
	end
	return res
end

function and(a,b | cond)
	
	# if a then b else a

	a -> cond
	while cond
		0 -> cond
		b -> a
	end
	return a
end

function or(a,b | cond,not_cond)

	# a or b = not(not a and not b)
	
	# not(a) -> a
	# not(b) -> b

	# and(a,b) -> res
	# not(res) -> res
	# return res
	
	# if a then a else b
	
	a -> cond
	1 -> not_cond
	
	while cond
		0 -> cond
		0 -> not_cond
	end
	
	while not_cond
		0 -> not_cond
		b -> a
	end
	
	return a
end

# Comparisons

function eq(a,b | cond)

	# Decrement simultaneously until one is 0
	
	and(a,b) -> cond
	while cond
		a-
		b-
		and(a,b) -> cond
	end
	
	# Are they both zero? not a and not b == not (a or b)
	
	or(a,b) -> cond
	not(cond) -> cond
	return cond
end

function gt(a,b | cond)
	
	# Decrement simultaneously until one is 0
	
	and(a,b) -> cond
	while cond
		a-
		b-
		and(a,b) -> cond
	end
	
	# Is a non zero?
	
	return a
end

function lt(a,b | cond)
	
	# Decrement simultaneously until one is 0
	
	and(a,b) -> cond
	while cond
		a-
		b-
		and(a,b) -> cond
	end
	
	# Is b non zero?
	
	return b
end


function main(a,b,c)
	
	lt(15,1) -> a
	eq(15,1) -> b
	gt(15,1) -> c?
end